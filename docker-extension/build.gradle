/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

plugins {
    id "com.github.johnrengelman.shadow"
}

apply from: "$rootDir/gradle/javaProject.gradle"

description = 'Ballerinax - Docker Extension'

configurations {
    fatJar {
        extendsFrom implementation
        exclude group: "io.netty"
        exclude group: "org.javassist"
        exclude group: "com.google.guava"
        exclude group: "com.fasterxml.jackson.core"
        exclude group: "com.fasterxml.jackson.dataformat"
        exclude group: "com.fasterxml.jackson.module"
        exclude group: "org.glassfish.hk2.external"
        exclude group: "org.bouncycastle"
        exclude group: "org.slf4j"
        exclude group: "commons-codec"
        exclude group: "org.apache.commons"
        exclude group: "commons-io"
        exclude group: "commons-logging"        
    }
}

dependencies {
    implementation project(':docker-generator')
    implementation "com.github.docker-java:docker-java:${dockerJavaVersion}"

    compileOnly "org.ballerinalang:ballerina-lang:${ballerinaLangVersion}"

    testImplementation "org.ballerinalang:ballerina-lang:${ballerinaLangVersion}"
    testImplementation "org.testng:testng:${testngVersion}"
}

jar {
    enabled = false
    dependsOn(shadowJar {
        project.configurations.implementation.canBeResolved = true
        configurations = [project.configurations.fatJar]
        classifier = null
    })
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact shadowJar
        }
    }
}

test {
    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}

build.dependsOn ':docker-generator:build'
